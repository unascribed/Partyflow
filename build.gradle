plugins {
	id 'java'
	id 'eclipse'
	id 'org.cadixdev.licenser' version '0.6.1'
	id 'com.github.ben-manes.versions' version '0.46.0'
}

repositories {
	mavenCentral()
	maven {
		url 'https://repo.unascribed.com'
	}
}

sourceCompatibility = 17
targetCompatibility = 17

archivesBaseName = 'Partyflow'

dependencies {
	implementation('org.eclipse.jetty:jetty-server:11.0.14') {
		exclude group: 'org.slf4j'
	}
	implementation 'org.apache.jclouds:jclouds-blobstore:2.5.0'
	implementation('org.apache.jclouds.driver:jclouds-slf4j:2.5.0') {
		transitive = false
	}
	
	implementation 'blue.endless:jankson:1.2.2'
	implementation 'com.google.guava:guava:31.1-jre'
	implementation 'com.github.spullara.mustache.java:compiler:0.9.10'
	implementation 'org.slf4j:slf4j-api:1.7.36'
	implementation 'com.unascribed:asyncsimplelog:4.6.1'
	implementation 'org.mariadb.jdbc:mariadb-java-client:3.1.3'
	implementation 'org.jsoup:jsoup:1.15.4'
	implementation 'org.commonmark:commonmark:0.21.0'
	implementation 'org.slf4j:jcl-over-slf4j:1.7.36'
	implementation 'org.apache.commons:commons-jexl3:3.3'
	
	// Storage drivers
	implementation 'org.apache.jclouds.api:filesystem:2.5.0'
	implementation 'org.apache.jclouds.api:s3:2.5.0'
	
	// Database drivers
	implementation 'com.h2database:h2:2.1.214'
	implementation 'org.mariadb.jdbc:mariadb-java-client:3.1.0'
}

jar {
	sourceSets.main.runtimeClasspath.each {
		if (it.isFile()) {
			from(zipTree(it)) {
				exclude 'META-INF/NOTICE*'
				exclude 'META-INF/CHANGES'
				exclude 'META-INF/DEPENDENCIES'
				exclude 'META-INF/README*'
				exclude 'META-INF/LICENSE*'
				exclude 'META-INF/mailcap.default'
				exclude '/module-info.class'
			}
		}
	}
	from 'LICENSE'
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	
	manifest.attributes (
		'Main-Class': 'com.unascribed.partyflow.Partyflow',
		'Implementation-Version': java.util.UUID.randomUUID().toString().replace("-", "")
	)
}

license {
	matching('**/com/unascribed/random/**') {
		header = file('headers/cc0.txt')
	}
	matching('**/com/lambdaworks/**') {
		header = file('headers/scrypt.txt')
	}
	matching('**/com/overzealous/**') {
		header = file('headers/remark.txt')
	}
	matching('**/blue/endless/jankson/**') {
		header = file('headers/jankson.txt')
	}
	header = file('headers/partyflow.txt')
	
	include '**/*.java'
}

compileJava {
	options.compilerArgs += "-parameters"
}

import com.unascribed.partyflow.TweakableGZIPOutputStream

task sourceZip(type: Zip) {
	dependsOn processResources
	destinationDirectory = file('build/resources/main/static')
	archiveBaseName = 'quine'
	reproducibleFileOrder = true
	entryCompression = 'stored'
	file('.gitignore').eachLine {
		if (!it.startsWith("#")) exclude(it)
	}
	exclude '.git'
	from(file('.')) {}
}

task precompressStaticResources {
	dependsOn sourceZip
	doLast {
		def files = [file('build/resources/main/static')]
		def nextFiles = []
		while (!files.empty) {
			for (def infile : files) {
				if (infile.directory) {
					for (def child : infile.listFiles()) {
						nextFiles.add(child)
					}
				} else if (!infile.getName().endsWith(".png")) {
					def outfile = new File(infile.toString()+".gz")
					infile.withInputStream { in ->
						outfile.withOutputStream { out ->
							new TweakableGZIPOutputStream(out).level(9).withCloseable { cout ->
								in.transferTo(cout)
							}
						}
					}
					infile.delete()
				}
			}
			files.clear()
			files.addAll(nextFiles)
			nextFiles.clear()
		}
	}
}

processResources.finalizedBy precompressStaticResources
