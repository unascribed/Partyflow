plugins {
	id 'java'
	id 'eclipse'
	id 'org.cadixdev.licenser' version '0.6.1'
	id 'com.github.ben-manes.versions' version '0.42.0'
}

repositories {
	mavenCentral()
	maven {
		url 'https://repo.unascribed.com'
	}
}

sourceCompatibility = 17
targetCompatibility = 17

archivesBaseName = 'Partyflow'

dependencies {
	implementation('org.eclipse.jetty:jetty-server:11.0.11') {
		exclude group: 'org.slf4j'
	}
	implementation 'org.apache.jclouds:jclouds-blobstore:2.5.0'
	implementation('org.apache.jclouds.driver:jclouds-slf4j:2.5.0') {
		transitive = false
	}
	implementation 'org.apache.jclouds.api:filesystem:2.5.0'
	implementation 'blue.endless:jankson:1.2.1'
	implementation 'com.google.guava:guava:31.1-jre'
	implementation 'com.h2database:h2:2.1.214'
	implementation 'com.github.spullara.mustache.java:compiler:0.9.10'
	implementation 'org.slf4j:slf4j-api:1.7.36'
	implementation 'com.unascribed:asyncsimplelog:4.6.1'
	implementation 'org.mariadb.jdbc:mariadb-java-client:3.0.7'
	implementation 'org.jsoup:jsoup:1.15.3'
	implementation 'org.commonmark:commonmark:0.19.0'
	implementation 'org.slf4j:jcl-over-slf4j:1.7.36'
	implementation 'org.apache.commons:commons-jexl3:3.2.1'
}

jar {
	sourceSets.main.runtimeClasspath.each {
		if (it.isFile()) {
			from(zipTree(it)) {
				exclude 'META-INF/NOTICE*'
				exclude 'META-INF/CHANGES'
				exclude 'META-INF/DEPENDENCIES'
				exclude 'META-INF/README*'
				exclude 'META-INF/LICENSE*'
				exclude 'META-INF/mailcap.default'
				exclude '/module-info.class'
			}
		}
	}
	from 'LICENSE'
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	
	manifest.attributes (
		'Main-Class': 'com.unascribed.partyflow.Partyflow'
	)
}

license {
	matching('**/com/unascribed/random/**') {
		header = file('headers/cc0.txt')
	}
	matching('**/com/lambdaworks/**') {
		header = file('headers/scrypt.txt')
	}
	matching('**/com/overzealous/**') {
		header = file('headers/remark.txt')
	}
	matching('**/blue/endless/jankson/**') {
		header = file('headers/jankson.txt')
	}
	header = file('headers/partyflow.txt')
	
	include '**/*.java'
}

task sourceZip(type: Zip) {
	destinationDirectory = file('src/main/resources/static')
	archiveBaseName = 'quine'
	reproducibleFileOrder = true
	file('.gitignore').eachLine { exclude(it) }
	exclude '.git'
	from(file('.')) {
	}
}

tasks.processResources.dependsOn sourceZip
